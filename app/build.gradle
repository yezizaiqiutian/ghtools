apply plugin: 'com.android.application'

android {

    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    buildToolsVersion rootProject.ext.android["buildToolsVersion"]

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
    }

    signingConfigs {
        release {
            keyAlias rootProject.ext.signing["keyAlias"]
            keyPassword rootProject.ext.signing["keyPassword"]
            storeFile file(rootProject.ext.signing["storeFile"])
            storePassword rootProject.ext.signing["storePassword"]
        }
        debug {
            keyAlias rootProject.ext.signing["keyAlias"]
            keyPassword rootProject.ext.signing["keyPassword"]
            storeFile file(rootProject.ext.signing["storeFile"])
            storePassword rootProject.ext.signing["storePassword"]
        }
    }

//    buildTypes {
//        release {
//            minifyEnabled false
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
//        }
//    }

    buildTypes {
        //正式包
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        //测试包
        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions rootProject.ext.flavors["dimension"]
    productFlavors {
        //生产环境
        online {
            buildConfigField "String", "BASE_URL", rootProject.ext.flavors["baseurl_online"]
            dimension rootProject.ext.flavors["dimension"]
            applicationId rootProject.ext.flavors["applicationId_online"]
            resValue "string", "app_name", rootProject.ext.flavors["appname_online"]
            manifestPlaceholders = [app_icon: rootProject.ext.flavors["appicon_online"]]
        }
        //个人运行
        debugenv {
            buildConfigField "String", "BASE_URL", rootProject.ext.flavors["baseurl_debugenv"]
            dimension rootProject.ext.flavors["dimension"]
            applicationId rootProject.ext.flavors["applicationId_debugenv"]
            resValue "string", "app_name", rootProject.ext.flavors["appname_debugenv"]
            manifestPlaceholders = [app_icon: rootProject.ext.flavors["appicon_debugenv"]]
        }
    }

}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation rootProject.ext.dependencies["appcompat-v7"]
    implementation rootProject.ext.dependencies["constraint"]

    implementation project(":netlib")
//    implementation project(":commonlib")

}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        def requested = details.requested
        if (requested.group == "com.android.support") {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion rootProject.ext.version["androidSupportSdkVersion"]
            }
        }
    }
}
